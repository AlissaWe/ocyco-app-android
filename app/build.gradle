apply plugin: 'com.android.application'

apply plugin: 'com.github.triplet.play'
play {
    track = 'beta'
    jsonFile = file('../play-keys.json')
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"
    defaultConfig {
        applicationId 'de.opencyclecompass.app.android'
        minSdkVersion 15
        targetSdkVersion 24
        versionCode getUnixTimestamp() as int
        versionName "1.0"
    }
    signingConfigs {
        if (System.getenv("TRAVIS")) {
            releaseSigning {
                storeFile rootProject.file('ocyco.keystore')
                storePassword System.getenv("KEYSTORE_PASS")
                keyAlias System.getenv("ALIAS_NAME")
                keyPassword System.getenv("ALIAS_PASS")
            }
            googlePlaySigning {
                storeFile rootProject.file('ocyco-google-play.keystore')
                storePassword System.getenv("PLAY_KEYSTORE_PASS")
                keyAlias System.getenv("PLAY_KEYSTORE_ALIAS")
                keyPassword System.getenv("PLAY_KEYSTORE_PASS")
            }
        }
    }
    buildTypes {
        if (System.getenv("TRAVIS")) {
            release {
                versionNameSuffix "-stable-" + getDate() + "-" + getCommit()
                minifyEnabled false
                shrinkResources false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                signingConfig signingConfigs.releaseSigning
            }
            bintray {
                applicationIdSuffix ".bintray"
                versionNameSuffix "-bintray-" + getDate() + "-" + getCommit()
                minifyEnabled false
                shrinkResources false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                signingConfig signingConfigs.releaseSigning
            }
            playStore {
                applicationIdSuffix ".googleplay"
                versionNameSuffix "-googleplay-" + getDate() + "-" + getCommit()
                minifyEnabled false
                shrinkResources false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                signingConfig signingConfigs.googlePlaySigning
            }
        } else {
            debug {
                applicationIdSuffix ".debug"
                versionNameSuffix "-debug-" + getDate() + "-" + getCommit()
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }
    }
    productFlavors {
    }
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmmss')
    return formattedDate
}

def getUnixTimestamp() {
    def date = new Date()
    def timestamp = Math.round(date.getTime() / 1000.0 )
    return timestamp
}

def getCommit() {
    if (System.getenv("TRAVIS_COMMIT")) {
        return System.getenv("TRAVIS_COMMIT")
    }
    else {
        return "local"
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:24.1.1'
    compile 'com.google.android.gms:play-services-location:9.2.1'
    compile 'org.slf4j:slf4j-android:1.7.21'
    compile 'org.osmdroid:osmdroid-android:4.3'
    compile 'com.google.code.gson:gson:2.7'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile files('libs/osmbonuspack_v5.1.jar')
    compile 'ch.acra:acra:4.6.2'
}
