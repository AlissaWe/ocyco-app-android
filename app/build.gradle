apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"
    defaultConfig {
        applicationId 'de.mytfg.jufo.ibis'
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    signingConfigs {
        if (System.getenv("TRAVIS")) {
            releaseSigning {
                storeFile rootProject.file('ibis.keystore')
                storePassword System.getenv("KEYSTORE_PASS")
                keyAlias System.getenv("ALIAS_NAME")
                keyPassword System.getenv("ALIAS_PASS")
            }
        }
    }
    buildTypes {
        if (System.getenv("TRAVIS")) {
            release {
                versionNameSuffix "-stable-" + getDate() + "-" + getCommit()
                minifyEnabled false
                shrinkResources false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                signingConfig signingConfigs.releaseSigning
            }
            bintray {
                applicationIdSuffix ".bintray"
                versionNameSuffix "-bintray-" + getDate() + "-" + getCommit()
                minifyEnabled false
                shrinkResources false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                signingConfig signingConfigs.releaseSigning
            }
        } else {
            debug {
                applicationIdSuffix ".debug"
                versionNameSuffix "-debug-" + getDate() + "-" + getCommit()
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }
    }
    productFlavors {
    }
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmmss')
    return formattedDate
}

def getCommit() {
    if (System.getenv("TRAVIS_COMMIT")) {
        return System.getenv("TRAVIS_COMMIT")
    }
    else {
        return "local"
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.google.android.gms:play-services-location:8.3.0'
    compile 'org.slf4j:slf4j-android:1.7.7'
    compile 'org.osmdroid:osmdroid-android:4.3'
    compile 'com.google.code.gson:gson:2.4'
    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile files('libs/osmbonuspack_v5.1.jar')
    compile 'ch.acra:acra:4.6.2'
}
